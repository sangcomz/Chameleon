apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android'

android {
    signingConfigs {
        if (project.rootProject.file('release/sign.properties').exists()) {
            Properties signProp = new Properties()
            signProp.load(project.rootProject.file('release/sign.properties').newDataInputStream())
            config {
                keyAlias signProp.get("alias") ? signProp.get("alias") : ""
                keyPassword signProp.get("password") ? signProp.get("password") : ""
                storeFile rootProject.file("release/key.jks")
                storePassword signProp.get("password") ? signProp.get("password") : ""
                v2SigningEnabled true
            }
        }
    }

    compileSdk gradle.compileSdk

    defaultConfig {
        applicationId "xyz.sangcomz.chameleonsample"
        minSdk gradle.minSdk
        targetSdk gradle.targetSdk
        versionName gradle.versionName
        versionCode gradle.versionCode
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.rootProject.file('release/sign.properties').exists()) {
                signingConfig signingConfigs.config
            }
        }

        debug {
        }
    }
    namespace 'xyz.sangcomz.chameleonsample'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            output.outputFileName = outputFile.name.replace('.apk', "-${versionName}.apk").replace('app-', "chameleon-")
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.constraintlayout:constraintlayout:$constraint_version"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroid_version"
    implementation "io.reactivex.rxjava2:rxjava:$rxJava_version"
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    implementation project(':chameleon')
}

play {
    jsonFile = file('play_key.json')
    track = 'beta' // Or any of 'rollout', 'beta', 'alpha' or 'internal'
}